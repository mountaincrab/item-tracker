plugins {
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.bossnutter'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

tasks.register("buildDockerImage", Exec) {
    group = "docker"
    executable = "docker"
    args = [
            "build",
            "--build-arg",
            "JAR_FILE=./build/libs/${project.name}-${project.version}.jar",
            "-t",
            "item-tracker:latest",
            "-t",
            "713056659575.dkr.ecr.eu-west-1.amazonaws.com/item-tracker:latest",
            "."
    ]
}

tasks.register("loginToEcr", Exec) {
    group = "docker"
    commandLine = ["bash", "-c", "aws ecr get-login-password --region eu-west-1 | docker login --username AWS --password-stdin 713056659575.dkr.ecr.eu-west-1.amazonaws.com"]
}

tasks.register("pushDockerImage", Exec) {
    group ="docker"
    dependsOn = ["loginToEcr"]
    executable = "docker"
    args = [
            "push",
            "713056659575.dkr.ecr.eu-west-1.amazonaws.com/item-tracker:latest"
    ]

}
